plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.elsiff'
version = '4.0.0'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    maven { url "https://papermc.io/repo/repository/maven-public/"}
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url "https://jitpack.io"}
    maven { url 'https://libraries.minecraft.net/' }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'com.mojang:brigadier:1.0.18'
    compileOnly 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'

    implementation('io.musician101.musicianlibrary:spigot:4.2') {
        exclude group: 'org.spigotmc'
    }
    implementation 'io.musician101:bukkitier:1.0'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
    }
}

shadowJar {
    dependencies {
        include dependency('io.musician101:')
        include dependency('io.musician101.musicianlibrary:')
    }

    archiveFileName = "more-fish-${project.version}.jar"
    relocate 'io.musician101.musicianlibrary', 'com.campmongoose.serversaturday.lib.io.musician101.musicianlibrary'
    relocate 'io.musician101.bukkitier', 'com.campmongoose.serversaturday.lib.io.musician101.bukkitier'
    dependsOn build
}
